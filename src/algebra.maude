fmod MATRIX-ALGEBRA is

    protecting INT .
    protecting FLOAT .
    protecting BOOL .
    protecting TRUTH-VALUE .
    protecting QID .
    sorts MatrixExpr MatrixSymbol Vector RowVector . 
    subsort Vector RowVector MatrixSymbol < MatrixExpr . 

    vars n m a b : Int .
    vars alpha beta : Float .

    vars A B C X Y : MatrixExpr .
    var S : MatrixSymbol .
    var M : Qid .

    *** Vector sorts are conditional 
    cmb A : Vector if (cols(A) == 1) .
    cmb A : RowVector if (rows(A) == 1) .

    *** MATRIX OPERATIONS
    ops I Zero : -> MatrixSymbol .
    op matrix : Qid Pair -> MatrixSymbol .

    op _+_ : MatrixExpr MatrixExpr -> MatrixExpr [ctor assoc comm prec 30] .
    op __ : MatrixExpr MatrixExpr -> MatrixExpr [ctor assoc prec 25] .
    op __ : Float MatrixExpr -> MatrixExpr [ctor left id: 1.0 prec 25] .
    op __ : Float Float -> Float [ctor assoc comm prec 25] .
    op _\_ : MatrixExpr MatrixExpr -> MatrixExpr [ctor assoc prec 25] .

    op transpose : MatrixExpr -> MatrixExpr [ctor] .
    op inverse : MatrixExpr -> MatrixExpr [ctor] .

    eq A (B + C) = (A B) + (A C) [metadata "Distributive Law"] . 

    eq alpha beta = alpha * beta [metadata "Rely on mul defined in FLOAT"] .
    ceq X (alpha Y) = alpha (X Y) if alpha =/= 1.0 [metadata "Scalars to Front"] .

    eq transpose(transpose(A)) = A .
    ceq transpose(alpha A) = alpha transpose(A) if alpha =/= 1.0 .
    *** eq transpose(A B) = transpose(B) transpose(A) .
    *** eq transpose(A + B) = transpose(A) + transpose(B) .

    eq inverse(inverse(A)) = A .
    eq inverse(A) A = I .
    eq A inverse(A) = I .
    eq I \ X = X .
    
    *** SHAPE
    sort Pair . 
    op _by_ : Int Int -> Pair [ctor prec 15].
    op _==_ : Pair Pair -> Bool .
    op first : Pair -> Int .
    op second : Pair -> Int .
    eq first( n by m ) = n . 
    eq second( n by m ) = m .
    vars P1 P2 : Pair .
    eq (P1 == P2) = (first(P1) == first(P2)) and (second(P1) == second(P2)) .

    op shape : MatrixExpr -> Pair .
    op rows : MatrixExpr -> Int .
    op cols : MatrixExpr -> Int .
    eq rows(A) = first(shape(A)) . 
    eq cols(A) = second(shape(A)) . 

    op is_square : MatrixExpr -> Bool . 
    eq is_square(X) = rows(X) == cols(X) .

    eq shape(matrix(M, n by m)) = n by m . 
    eq shape(A B) = rows(A) by cols(B) .
    eq shape(A + B) = shape(A) .
    eq shape(transpose(A)) = cols(A) by rows(A) .
    eq shape(inverse(A)) = shape(A) .
    eq shape(A \ B) = cols(A) by cols(B) .
    ceq shape(alpha A) = shape(A) if alpha =/= 1.0 .

    op valid : MatrixExpr -> Bool .
    eq valid(S) = true . *** all symbols are valid
    eq valid(X Y) = valid(X) and valid(Y) and (cols(X) == rows(Y)) .
    eq valid(X + Y) = valid(X) and valid(Y) and (shape(X) == shape(Y)) .
    eq valid(transpose(X)) = valid(X) .
    eq valid(inverse(X)) = valid(X) and is_square(X) . 
    eq valid(X \ Y) = rows(X) == rows(Y) and valid(X) and valid(Y) .
    ceq valid(alpha X) = valid(X) if alpha =/= 1.0 . 

    *** Catch alls
    eq valid(X) = false [owise] .
endfm
